' 【注意】网页模板/HTTP头模板在常量中，txt版本没有显示，如需查看请打开“网页下载服务.e”
.版本 2
.支持库 eAPI
.支持库 edroptarget
.支持库 spec
.支持库 commobj

.程序集 窗口程序集_启动窗口
.程序集变量 全部HTTP状态, 文本型, , "0"
.程序集变量 已添加网址, 文本型, , "0"
.程序集变量 超时时间, 整数型, , , 单位秒
.程序集变量 软件目录, 文本型
.程序集变量 设置_目标文件名, 文本型
.程序集变量 设置_目标目录, 文本型

.子程序 __启动窗口_创建完毕
.局部变量 读入设置, 文本型

全部HTTP状态 ＝ 分割文本 (#HTTP_全部状态, #换行符, )
超时时间 ＝ 10
读入设置 ＝ 到文本 (读入文件 (取运行目录 () ＋ “\wds.ini”))
设置_目标文件名 ＝ 文本_取出中间文本 (读入设置, “<1>”, “<2>”)
设置_目标目录 ＝ 文本_取出中间文本 (读入设置, “<2>”, “<3>”)
.如果真 (设置_目标文件名 ＝ “” 或 设置_目标目录 ＝ “”)
    初始设置 ()
    返回 ()
.如果真结束
时钟1.时钟周期 ＝ 500  ' 防止在保存后启动时判断了重复运行

.子程序 _时钟1_周期事件

时钟1.时钟周期 ＝ 0
.如果真 (取进程运行数量 (取执行文件名 ()) ＞ 1)
    干掉另一个自己 ()  ' 发现运行第二个实例时启动设置界面
    初始设置 ()
    返回 ()
.如果真结束
服务器1.端口 ＝ 15651

.子程序 干掉另一个自己
.局部变量 A233, 进程信息, , "0", 数组
.局部变量 计次, 整数型, , , 数组多少

A233 ＝ 取系统进程列表 ()
.变量循环首 (1, 取数组成员数 (A233), 1, 计次)
    .如果真 (到文本 (A233 [计次].进程名称) ＝ 取执行文件名 ())
        .如果真 (A233 [计次].进程标识符 ≠ GetCurrentProcessId ())
            终止进程 (A233 [计次].进程标识符)
        .如果真结束

    .如果真结束

.变量循环尾 ()

.子程序 初始设置

拖放对象1.注册拖放控件 (_启动窗口.取窗口句柄 ())
_启动窗口.可视 ＝ 真

.子程序 _服务器1_数据到达
.局部变量 当前客户, 文本型
.局部变量 已次数, 整数型
.局部变量 当前Ex网址, 文本型
.局部变量 读出文本, 文本型
.局部变量 当前列表, 文本型, , "0"

当前客户 ＝ 服务器1.取回客户 ()
.如果真 (文本_取出中间文本 (“?” ＋ 当前客户, “?”, “:”) ≠ “127.0.0.1”)
    服务器1.发送数据 (当前客户, HTTP_加头 (#HTTP_403, 403), 超时时间)  ' 若非本地访问则直接返回403
    返回 ()
.如果真结束
' 编辑框1.加入文本 (到文本 (服务器1.取回数据 ()) ＋ #换行符)
当前Ex网址 ＝ HTTP_输入取ExURL (到文本 (服务器1.取回数据 ()))
.如果真 (当前Ex网址 ＝ “”)
    服务器1.发送数据 (当前客户, HTTP_加头 (#HTTP_404, 404), 超时时间)
    返回 ()
.如果真结束
.如果真 (当前Ex网址 ＝ “500”)
    服务器1.发送数据 (当前客户, HTTP_加头 (#HTTP_500, 500), 超时时间)
    返回 ()
.如果真结束
.计次循环首 (取数组成员数 (已添加网址), 已次数)
    .如果真 (已添加网址 [已次数] ＝ 当前Ex网址)
        服务器1.发送数据 (当前客户, HTTP_加头 (HTTP_信息框网页生成 (“此地址已添加过”), 200), 超时时间)
        返回 ()
    .如果真结束

.计次循环尾 ()
.如果真 (取进程运行数量 (设置_目标文件名) ＝ 0)
    .如果真 (文件是否存在 (设置_目标目录 ＋ 设置_目标文件名) ＝ 假)
        服务器1.发送数据 (当前客户, HTTP_加头 (HTTP_信息框网页生成 (“WebDownloadService找不到目标” ＋ 设置_目标文件名 ＋ “文件，无法运行ExDownloader”), 200), 超时时间)
        返回 ()
    .如果真结束
    读出文本 ＝ 到文本 (读入文件 (设置_目标目录 ＋ “list.txt”))
    当前列表 ＝ 分割文本 (读出文本, #换行符, )
    .计次循环首 (取数组成员数 (当前列表), 已次数)
        .如果真 (当前列表 [已次数] ＝ 当前Ex网址)
            服务器1.发送数据 (当前客户, HTTP_加头 (HTTP_信息框网页生成 (“列表中已存在此地址”), 200), 超时时间)
            返回 ()
        .如果真结束

    .计次循环尾 ()
    读出文本 ＝ 读出文本 ＋ 当前Ex网址 ＋ #换行符
    调试输出 (到文本 (到字节集 (读出文本)), “保存文件”, 设置_目标目录 ＋ “list.txt”)
    .如果真 (写到文件 (设置_目标目录 ＋ “list.txt”, 到字节集 (读出文本)) ＝ 假)
        服务器1.发送数据 (当前客户, HTTP_加头 (HTTP_信息框网页生成 (“WebDownloadService写ExDownloader列表文件失败”), 200), 超时时间)
        返回 ()
    .如果真结束
    .如果 (运行 (设置_目标目录 ＋ 设置_目标文件名, 假, 3) ＝ 真)
        服务器1.发送数据 (当前客户, HTTP_加头 (HTTP_信息框网页生成 (), 200), 超时时间)
        加入成员 (已添加网址, 当前Ex网址)
    .否则
        服务器1.发送数据 (当前客户, HTTP_加头 (HTTP_信息框网页生成 (“WebDownloadService启动ExDownloader失败”), 200), 超时时间)
    .如果结束
    返回 ()
.如果真结束
.如果真 (客户1.连接 (“127.0.0.1”, 15650) ＝ 假)
    服务器1.发送数据 (当前客户, HTTP_加头 (HTTP_信息框网页生成 (“WebDownloadService连接ExDownloader失败”), 200), 超时时间)
    返回 ()
.如果真结束
调试输出 (当前Ex网址, “发送网址”)
.如果真 (客户1.发送数据 (当前Ex网址) ＝ 假)
    服务器1.发送数据 (当前客户, HTTP_加头 (HTTP_信息框网页生成 (“WebDownloadService给ExDownloader发送数据失败”), 200), 超时时间)
    返回 ()
.如果真结束
客户1.断开连接 ()
加入成员 (已添加网址, 当前Ex网址)
服务器1.发送数据 (当前客户, HTTP_加头 (HTTP_信息框网页生成 (), 200), 超时时间)

.子程序 HTTP_输入取ExURL, 文本型
.参数 输入头, 文本型
.局部变量 输入内容, 文本型

输入内容 ＝ 文本_取出中间文本 (输入头, “GET /addurl/geturl.wehs?url=”, “ HTTP/”)
输入内容 ＝ 编码_URL解码 (输入内容)
输入内容 ＝ 文本_替换 (输入内容, , , , 文本_取出中间文本 (输入内容 ＋ “\”, “?”, “\”), “”)  ' 去掉页码
.如果真 (快速取出现次数 (输入头, “/addurl/geturl.wehs”) ≠ 0 且 输入内容 ＝ “”)
    返回 (“500”)
.如果真结束
返回 (输入内容)

.子程序 快速取出现次数, 整数型
.参数 全部文本, 文本型
.参数 要找的文本, 文本型
.局部变量 全部内容, 快速文本对象

全部内容.置文本 (全部文本)
返回 (全部内容.替换子文本 (要找的文本, “”, , , 真))

.子程序 取进程运行数量, 整数型
.参数 进程文件名称, 文本型
.局部变量 进程, 进程信息, , "0"
.局部变量 内已次数, 整数型
.局部变量 进程数量, 整数型

进程 ＝ 取系统进程列表 ()
.计次循环首 (取数组成员数 (进程), 内已次数)
    .如果真 (进程 [内已次数].进程名称 ＝ 进程文件名称)
        进程数量 ＝ 进程数量 ＋ 1
    .如果真结束

.计次循环尾 ()
返回 (进程数量)

.子程序 HTTP_信息框网页生成, 文本型
.参数 信息内容, 文本型, 可空

.如果真 (信息内容 ＝ “”)
    返回 (#HTTP_关闭网页)
.如果真结束
返回 (文本_替换 (#HTTP_信息框, , , , “{内容}”, 信息内容))

.子程序 HTTP_加头, 文本型
.参数 输入网页, 文本型
.参数 HTTP状态, 整数型, 可空, 默认200
.局部变量 已次数, 整数型
.局部变量 当前状态, 文本型
.局部变量 网页长度, 整数型
.局部变量 输出头部, 文本型

.如果真 (取文本长度 (到文本 (HTTP状态)) ≠ 3)
    HTTP状态 ＝ 200
.如果真结束
.计次循环首 (取数组成员数 (全部HTTP状态), 已次数)
    .如果真 (到文本 (HTTP状态) ＝ 取文本左边 (全部HTTP状态 [已次数], 3))
        当前状态 ＝ 全部HTTP状态 [已次数]
        跳出循环 ()
    .如果真结束

.计次循环尾 ()
网页长度 ＝ 取文本长度 (输入网页)
输出头部 ＝ 文本_替换 (#HTTP_头, , , , “{状态}”, 当前状态, “{长度}”, 到文本 (网页长度))
调试输出 (输出头部)
返回 (输出头部 ＋ 输入网页)

.子程序 _拖放对象1_得到文件
.参数 接收到的文件路径, 文本型
.局部变量 保存设置, 文本型

.如果真 (到小写 (文件_取扩展名 (接收到的文件路径)) ≠ “.exe”)
    返回 ()
.如果真结束
保存设置 ＝ “<1>” ＋ 文件_取文件名 (接收到的文件路径, 真) ＋ “<2>” ＋ 文件_取目录 (接收到的文件路径) ＋ “<3>”
写到文件 (取运行目录 () ＋ “\wds.ini”, 到字节集 (保存设置))
.如果真 (设置自动运行 (“WebDownloadService”, 取运行目录 () ＋ “\” ＋ 取执行文件名 (), #接口常量.到注册表) ＝ 假)
    .如果真 (设置自动运行 (“WebDownloadService”, 取运行目录 () ＋ “\” ＋ 取执行文件名 (), #接口常量.到启动组) ＝ 假)
        信息框 (“注册开机启动失败！”, 0, , )
    .如果真结束

.如果真结束
信息框 (“初始化成功！” ＋ #换行符 ＋ “注意，若软件文件名变更(例如更新软件)，则需要重新打开本软件拖入软件来初始化！”, 0, , )
运行 (取运行目录 () ＋ “\” ＋ 取执行文件名 (), 假, )
结束 ()
